# Maintainer: nexus <nexus-x[at]tuta[dot]io> 

pkgname="hardened-malloc"
pkgver=13+r730+g7481c8857
pkgrel=1
pkgdesc="Hardened allocator designed for modern systems."
url="https://github.com/GrapheneOS/hardened_malloc"
license=("MIT")
depends=('gcc-libs')
makedepends=('git')
checkdepends=('python')
provides=('libhardened_malloc.so' 'libhardened_malloc-light.so')
arch=("x86_64")
source=('hardened_malloc::git+https://github.com/GrapheneOS/hardened_malloc#branch=main')
sha256sums=('SKIP')

pkgver(){
  cd "$srcdir/hardened_malloc"
  _version=$(git tag --sort=refname --list | grep "^[0-9]\{2,4\}$" | sort -nr | head -1)
  _commits=$(git rev-list --count HEAD)
  _short_commit_hash=$(git rev-parse --short=9 HEAD)
  echo "${_version}+r${_commits}+g${_short_commit_hash}"
}

build() {
  cd "${srcdir}/hardened_malloc"
  make VARIANT=default
  make VARIANT=light
}

package() {
	cd "${srcdir}/hardened_malloc"
	install -vDm755 -t "$pkgdir/usr/lib" out/libhardened_malloc.so out-light/libhardened_malloc-light.so
        install -vDm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
