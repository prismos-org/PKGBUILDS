# Original AUR package: https://aur.archlinux.org/packages/mullvad-browser-bin modified to be used In Prism OS
# Maintainer: nexus <nexus-x[at]tuta[dot]io> 

# Fetching the signing key:
#     https://mullvad.net/en/help/verifying-mullvad-browser-signature
# If you can't open the page, here's the important line:
#     gpg --auto-key-locate nodefault,wkd --locate-keys torbrowser@torproject.org

pkgname=mullvad-browser
pkgver=14.5.5
pkgrel=1
pkgdesc='Privacy-focused web browser developed by Mullvad and the Tor Project'
arch=(x86_64)
url=https://mullvad.net/en/browser
license=(GPL-3.0-or-later MPL-2.0)
depends=(
  alsa-lib
  at-spi2-core
  bash
  cairo
  dbus
  dbus-glib
  fontconfig
  freetype2
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  gtk3
  hicolor-icon-theme
  libpulse
  libx11
  libxcb
  libxcomposite
  libxcursor
  libxdamage
  libxext
  libxfixes
  libxi
  libxrandr
  libxrender
  libxss
  libxt
  libxtst
  mime-types
  nspr
  nss
  pango
  ttf-font
)
optdepends=(
  'hunspell-en_US: Spell checking, American English'
  'libnotify: Notification integration'
  'networkmanager: Location detection via available WiFi networks'
  'pulse-native-provider: Sound support when using PipeWire or PulseAudio' # thanks to @cyberpunkrocker for the suggestion
  'speech-dispatcher: Text-to-Speech'
  'xdg-desktop-portal: Screensharing with Wayland'
)
provides=(mullvad-browser=$pkgver mullvad-browser)
conflicts=(mullvad-browser)

source=(
  https://cdn.mullvad.net/browser/$pkgver/mullvad-browser-linux-x86_64-$pkgver.tar.xz{,.asc}
  mullvad-browser.sh
  mullvad-browser.desktop
)
validpgpkeys=(
  'EF6E286DDA85EA2A4BA7DE684E2C6E8793298290' # Tor Browser Developers (signing key) <torbrowser@torproject.org>
)
changelog='mullvad-browser.changelog'

sha256sums=('3f0ab1632956eb4d9700a06f109938465f2aea7581187de916f4ceb8062daffc'
	    'b8e47102d201dc43ce6d531b7399b1ba3c9a43e370668374e77a769a00762f4b'
	    'dba81d84965e19e9a7ce014a2597a8937b915d8eb82a7f06e52501f53749d9f2'
            'd2441b3868312707e36579adfd13903217af1b3187e0505525276ef3cbe9c5b9')
package() {
  local pkg=${pkgname%%-bin}

  # cli wrapper
  install -Dvm755 "$pkg.sh" "$pkgdir/usr/bin/$pkg"

  # desktop file
  install -Dvm644 "$pkg.desktop" -t "$pkgdir/usr/share/applications/"

  cd mullvad-browser

  # only owner has access to all files
  chmod --recursive --verbose a+r .
  find . -executable -execdir chmod --verbose a+x '{}' +

  # copy files from archive
  install -dvm755 "$pkgdir/opt/$pkg/"
  cp --archive --verbose Browser/. "$pkgdir/opt/$pkg/"

  # fix "open file" dialog crash
  sed -i 's|<dir prefix="cwd">fonts</dir>|<dir>/opt/mullvad-browser/fonts</dir>|' \
    "$pkgdir/opt/$pkg/fonts/fonts.conf"

  # create profiles in ~
  install -Dvm644 /dev/null "$pkgdir/opt/$pkg/system-install"

  # disable built-in updates. See: https://github.com/mullvad/mullvad-browser/issues/229
  install -Dvm644 /dev/null "$pkgdir/opt/$pkg/is-packaged-app"

  # icons
  local size
  for size in 16 32 48 64 128; do
    install -Dvm644 "$pkgdir/opt/$pkg/browser/chrome/icons/default/default$size.png" \
      "$pkgdir/usr/share/icons/hicolor/${size}x${size}/apps/$pkg.png"
  done
  install -Dvm644 "$pkgdir/opt/$pkg/browser/chrome/icons/default/about-logo.svg" \
      "$pkgdir/usr/share/icons/hicolor/scalable/apps/$pkg.svg"

  # license files
  install -dvm755 "$pkgdir/usr/share/licenses/"
  ln -sfv "/opt/$pkg/MullvadBrowser/Docs/Licenses" "$pkgdir/usr/share/licenses/$pkg"
}
